/*
 * Copyright 2022 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.springframework.boot' version '3.4.4'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'idea'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.google.cloud.solutions'
version = '0.1.1-SNAPSHOT'

def floggerVersion = "0.8"
def truthVersion = "1.4.4"
def protobufVersion = "4.30.2"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    // Commons library deps
    implementation "com.google.guava:guava:33.4.6-jre"
    implementation "com.google.code.gson:gson:2.12.1"

    // Logging framework
    implementation "com.google.flogger:flogger:${floggerVersion}"
    implementation "com.google.flogger:google-extensions:${floggerVersion}"
    runtimeOnly "com.google.flogger:flogger-system-backend:${floggerVersion}"

    // Google Cloud Service Client Libraries
    implementation "com.google.cloud:google-cloud-dlp:3.64.0"

    //Spring deps
    implementation ("org.springframework.boot:spring-boot-starter-web:") {
        exclude group: "org.yaml", module: "snakeyaml"
        exclude group: "org.springframework.boot", module: "spring-boot-starter-json"
    }

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Test Deps
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.12.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.12.1"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // proto json format
    testImplementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    testImplementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    testImplementation "com.google.truth.extensions:truth-proto-extension:${truthVersion}"

}

jib {
    from {
        image = 'eclipse-temurin:21-jre@sha256:8802b9e75cfafd5ea9e9a48fb4e37c64d4ceedb929689b2b46f3528e858d275f'
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.encoding = "UTF-8"
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnit()
    dependsOn cleanTest
    finalizedBy jacocoTestReport
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = TestExceptionFormat.FULL
    }
}

jacocoTestReport {
    reports {
        csv.required = false
        xml.required = true
        html.required = false
    }
}

def licenseLines = new File("ApacheHeader").readLines()
def licenseJava = "/*\n" + licenseLines.collect(ln -> " *" + (ln.matches("^.+")? " " : "") + ln).join("\n") + "\n */\n\n"
def licenseWithForwardSlashes = "//\n" + licenseLines.collect(ln -> "//" + (ln.matches("^.+")? " " : "") + ln).join("\n") + "\n//\n\n"

spotless {
    java {
        target "**/*.java"
        targetExclude "third_party/**"
        googleJavaFormat('1.18.1')
        removeUnusedImports()
        importOrder()
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeader(licenseJava)
    }

    kotlin {
        target "**/*.kt"
        ktlint()
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
        licenseHeader(licenseJava)
    }

    freshmark {
        target '*.md'
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format "proto", {
        target "**/*.proto"
        licenseHeader(licenseWithForwardSlashes, "syntax")
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }


    format "gradle", {
        target "**/*.gradle"
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeader(licenseJava, "(import|plugins|rootProject)")
    }

    format "misc", {
        target "**/.gitignore", "*.xml", "src/**/*.xml"
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }
}
